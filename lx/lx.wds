$$ Define helper aliases for easy maintenance of the structure offsets
.block { aS G_HeaderSize         @@masm(@$ptrsize)                                                 }
.block { aS G_InstanceListOffset (${G_HeaderSize})                                                 }

.block
{
    $$ Detect RS2 systems (with no symbols for now)
    .if (dwo(fffff780`00000260) >= 38a4)
    {
        $$ Set the root global pointer
        r @$t0 = lxcore!LxpRoutines - 4 * @$ptrsize
    }
    .else
    {
        $$ Set the root global pointer
        r @$t0 = lxcore!LxGlobal
    }

    $$ Print information on the LX_GLOBAL_DATA structure
    .printf /D "<b><u>Lx Global Data 0x%P\n\n</u></b>", @$t0
    .printf "NTC: %lx (0x15c1 expected), Size: %lx bytes\n", wo(@$t0), wo(@$t0 + 2)

    $$ Loop the list of sessions inside of this instance
    r? @$t1 = (nt!_LIST_ENTRY*)(@$t0 + ${G_InstanceListOffset})
    .for (r? @$t2 = @$t1->Flink; @$t2 != @$t1; r? @$t2 = @$t2->Flink)
    {
        $$ Dump each LX_SESSION in the list
        .push /r /q
        $$>a< c:\lx\lx-instance.wds @$t2
        .pop /r /q
    }
}

ad *